plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

ext {
	set('signing.keyId',System.getenv('signing.keyId'))
	set('signing.password',System.getenv('signing.password'))
	set('signing.secretKeyRingFile',System.getenv('signing.secretKeyRingFile'))
	set('deployUsername',System.getenv('NEXUS_OSS_USER_NAME'))
	set('deployPassword',System.getenv('NEXUS_OSS_PASS_WORD'))
}

configure(allprojects) { project ->

	apply plugin: 'java'

	version = VERSION
	group = GROUPID

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		//加上阿里云镜像
		maven { url 'https://maven.aliyun.com/repository/central' }
		maven { url 'https://maven.aliyun.com/repository/public' }
		maven { url "https://maven.aliyun.com/repository/spring" }
		mavenCentral()
		gradlePluginPortal()
		maven { url "https://repo.spring.io/release" }
	}
}

configure(subprojects) { project ->

	apply from: "${rootDir}/gradle/publications.gradle"

	javadoc {
		description = "Generates project-level javadoc for use in -javadoc jar"

		options.encoding = "UTF-8"
		options.charSet = "UTF-8"
		options.memberLevel = JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = project.name
		options.use = true
		options.addStringOption("Xdoclint:none", "-quiet")

		// Suppress warnings due to cross-module @see and @link references.
		// Note that global 'api' task does display all warnings.
		logging.captureStandardError LogLevel.INFO
		logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
	}
}